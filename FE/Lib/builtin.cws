@builtin (module [] :

@doc "macro for while-loop"
@pub (macro while STMT [(mparam $cond EXPR) (mparam $body STMT_LIST)] [] :
    (block _ :
        (if $cond :
         :
            (break))
        $body
        (continue)))


@doc """macro for number range for-loop,

The type of the loop variable is determined by $end"""
@pub (macro for STMT_LIST [
        (mparam $index ID)
        (mparam $start EXPR)
        (mparam $end EXPR)
        (mparam $step EXPR)
        (mparam $body STMT_LIST)] [$end_eval $step_eval $it] :
    (mlet $end_eval (type_of $end) $end)
    (mlet $step_eval (type_of $end) $step)
    (mlet! $it (type_of $end) $start)
    (block _ :
        (if (>= $step_eval 0) :
            (if (>= $it $end_eval) :
                (break)
             :)
         :
            (if (<= $it $end_eval) :
                (break)
             :))
        (mlet $index auto $it)
        (= $it (+ $it $step_eval))
        $body
        (continue)))


@pub (macro trylet STMT_LIST [
        (mparam $name ID)
        (mparam $type EXPR)
        (mparam $expr EXPR)
        (mparam $catch_name ID)
        (mparam $catch_body STMT_LIST)] [$eval] :
    (mlet $eval auto $expr)
    (if (is $eval $type) :
     :
        (mlet $catch_name auto (@unchecked narrow_as $eval (union_delta (type_of $eval) $type)))
        $catch_body
        (trap))
    (mlet $name $type (@unchecked narrow_as $eval $type)))


@pub (macro trylet! STMT_LIST [
        (mparam $name ID)
        (mparam $type EXPR)
        (mparam $expr EXPR)
        (mparam $catch_name ID)
        (mparam $catch_body STMT_LIST)] [$eval] :
    (mlet $eval auto $expr)
    (if (! (is $eval $type)) :
        (mlet $catch_name auto (@unchecked narrow_as $eval (union_delta (type_of $eval) $type)))
        $catch_body
        (trap)
     :)
    (mlet! $name $type (@unchecked narrow_as $eval $type)))


@pub (macro tryset STMT_LIST [
        (mparam $lhs EXPR)
        (mparam $expr EXPR)
        (mparam $catch_name ID)
        (mparam $catch_body STMT_LIST)] [$eval] :
    (mlet $eval auto $expr)
    (if (! (is $eval (type_of $lhs))) :
        (mlet $catch_name auto (@unchecked narrow_as $eval (union_delta (type_of $eval) (type_of $lhs))))
        $catch_body
        (trap)
     :)
    (= $lhs (narrow_as $eval (type_of $lhs))))


(macro swap# STMT_LIST [(mparam $a EXPR) (mparam $b EXPR)] [$t] :
    (mlet $t auto $a)
    (= $a $b)
    (= $b $t))


@doc "works with arrays and slices. For arrays we make sure we do not copy them."
@pub (macro span_inc EXPR [(mparam $slice EXPR) (mparam $size EXPR)] [$orig_size $orig_len $orig_slice] :
    (expr :
        (mlet $orig_slice auto $slice)
        (mlet $orig_len auto (len $orig_slice))
        (mlet $orig_size uint $size)
        (if (> $orig_size $orig_len) :
            (trap)
         :)
        (return (span_val (pinc (@preserve_mut front $orig_slice) $orig_size) (- $orig_len $orig_size)))))

@doc "works with arrays and slices. For arrays we make sure we do not copy them."
@pub (macro span_truncate_or_die# EXPR [(mparam $slice EXPR) (mparam $size EXPR)] [$orig_size $orig_len $orig_slice] :
    (expr :
        (mlet $orig_slice auto (as $slice (span! (type_of (^ (front $slice))))))
        (mlet $orig_len auto (len $orig_slice))
        (mlet $orig_size uint $size)
        (if (> $orig_size $orig_len) :
            (trap)
         :)
        (return (span_val (front! $orig_slice) $orig_size))))


@doc "works with arrays and slices. For arrays we make sure we do not copy them."
@pub (macro span_append_or_die# EXPR [(mparam $slice EXPR) (mparam $out EXPR)] [$e_slice $e_out] :
    (expr :
        (mlet $e_slice (span (type_of (^ (front $slice)))) $slice)
        (mlet! $e_out (span! (type_of (^ (front $out)))) $out)
        (if (> (len $e_slice) (len $e_out)) :
            (trap)
         :)
        (for i 0 (len $e_slice) 1 :
            (= (at $e_out i) (at $e_slice i)))
        (return (len $e_slice))))


@pub @extern @cdecl (fun print_ln [(param s (ptr u8)) (param size uint)] void :)


@doc "simple assert for those libs that cannot import fmt"
@pub (macro assert# STMT [(mparam $cond EXPR) (mparam $text EXPR)] [$e_cond $e_text] :
    (if $cond :
     :
        (mlet $e_cond (span u8) (stringify $cond))
        (mlet $e_text (span u8) $text)
        (do (call print_ln [(front $e_cond) (len $e_cond)]))
        (do (call print_ln [(front $e_text) (len $e_text)]))
        (trap)))
)
